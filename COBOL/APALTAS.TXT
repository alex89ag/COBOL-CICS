000100 IDENTIFICATION DIVISION.
000200 PROGRAM-ID. APALTAS.
000300 ENVIRONMENT DIVISION.
000400 DATA DIVISION.
000500 WORKING-STORAGE SECTION.
000600 01  RESPUESTA          PIC S9(8) COMP.
000700 01  MSGFATAL           PIC X(20) VALUE 'ERROR DE SEGURIDAD!!'.
000800 01  WS-INPUT.
000900     05   WS-TRAN-ID        PIC X(4).
001000     05   WS-MESSAGE-I      PIC X(70).
001100 01  WS-OUTPUT.
001200     05    WS-TEXT          PIC X(8).
001300     05    WS-MESSAGE-O     PIC X(70).
001400 01  WS-MSG-LENGTH          PIC S9(4) COMP.
001500 01  WS-NROCLI              PIC X(10).
001600 01  WS-FECHA.
001700     05 WS-FECA             PIC 99.
001800     05 WS-FECM             PIC 99.
001900     05 WS-FECD             PIC 99.
002000 01  WS-OLD-DIG             PIC 9.
002010 01  WS-PROG                PIC 9.
002020 01 PARAM-REG.
002030     05 PARAM-CLAVE.
002040        10 PARAM-TIP     PIC 9.
002050        10 PARAM-NRO     PIC 9(8).
002060     05 PARAM-NRO-CLI    PIC 9(8).
002070     05 FILLER           PIC X(63).
002100 01  LK-DIGITOVE            PIC 9.
002200 01  LK-COMMAREA.
002300     05 LK-CICLO            PIC 9.
002400     05 LK-NROCLI.
002500        10 LK-NC-TIP        PIC 9.
002600        10 LK-NC-NRO.
002700           15 LK-NCN-NRO       PIC 9(8).
002800           15 LK-NCN-DIG       PIC 9.
002900     05 LK-MSG    PIC X(31).
002910 01  LK-COMMAREA-H.
002920     05  LK-CICLO-H  PIC 9.
002930     05  LK-PROG-H   PIC X(8).
002940     05  LK-OPCI-H   PIC 9.
003000     COPY DFHAID.
003100     COPY DFHBMSCA.
003200     COPY AALTAS.
003210     COPY MAECLIE.
003300
003400 LINKAGE SECTION.
003500 01  DFHCOMMAREA.
003600     05 COM-CICLO            PIC 9.
003700     05 COM-NROCLI          PIC 9(10).
003800     05 FILLER               PIC X(89).
003900
004000 PROCEDURE DIVISION.
004100 0000-INICIO.
004200     MOVE LOW-VALUES TO ALTASO
004300     EXEC CICS IGNORE CONDITION ERROR
004400     END-EXEC.
004500
004700     MOVE DFHCOMMAREA TO LK-COMMAREA
004800     MOVE LK-MSG(1:1) TO WS-PROG
004900     IF EIBCALEN = 0
005000         EXEC CICS SEND FROM(MSGFATAL) LENGTH(20) ERASE
005100         END-EXEC
005200         EXEC CICS ABEND ABCODE('COSA')
005300         END-EXEC
005400     END-IF.
005500     EVALUATE LK-CICLO
005600         WHEN 1 PERFORM CICLO1
005700         WHEN 2 PERFORM CICLO2
005800         WHEN 3 PERFORM CICLO3
005900     END-EVALUATE.
006000     EXEC CICS RETURN TRANSID('AALT') COMMAREA(LK-COMMAREA)
006100          LENGTH(12) END-EXEC.
006200
006300 CICLO1.
006310     PERFORM ENCABEZADO
006400     EXEC CICS SEND MAP('ALTAS') MAPSET('AALTAS') ERASE FREEKB
006500          RESP(RESPUESTA)
006600     END-EXEC
006700     MOVE 2  TO LK-CICLO.
006900 CICLO2.
006910     PERFORM ENCABEZADO
007000     EXEC CICS RECEIVE MAP('ALTAS') MAPSET('AALTAS')
007100          RESP(RESPUESTA)
007200     END-EXEC.
007300     IF EIBAID = DFHPF3
007310         PERFORM RETORNO
007800     ELSE
007900         IF RESPUESTA  = DFHRESP(NORMAL)
007901             EVALUATE EIBAID
007910                 WHEN DFHPF1
007920                     MOVE ZERO TO LK-OPCI-H
007930                     PERFORM HELP
007940                 WHEN DFHENTER
007942                     PERFORM MDT-SET
007943*                    PERFORM VALIDAR
007944                     PERFORM NRO-CLIENTE
007945                     PERFORM GUARDAR-CLIENTE
007946                     IF RESPUESTA = DFHRESP(NORMAL)
007947                         PERFORM RW-NRO-CLIENTE
007948                         MOVE 'CLIENTE GUARDADO' TO MSGALTO
007949                     ELSE
007957                         MOVE 'ERRRO AL GRABAR'  TO MSGALTO
007958                     END-IF
007959                     PERFORM CICLO1
007960                 WHEN OTHER
008000                     MOVE 'OPCION INVALIDA' TO MSGALTO
008100                     PERFORM CICLO1
008110             END-EVALUATE
010000         END-IF
010100     END-IF.
010114
010115 CICLO3.
010116     PERFORM ENCABEZADO
010117     MOVE LK-MSG TO MSGALTO.
010118     EXEC CICS SEND MAP('ALTAS') MAPSET('AALTAS') ERASE FREEKB
010119          RESP(RESPUESTA)
010120     END-EXEC.
010121     MOVE 2 TO LK-CICLO.
010122
010123 MDT-SET.
010124     MOVE DFHBMFSE TO CLI-TIP
010125     MOVE DFHBMFSE TO CLI-NRO
010126     MOVE DFHBMFSE TO CLINOMA
010127     MOVE DFHBMFSE TO CLIAPEA
010128     MOVE DFHBMFSE TO CLITPDA
010129     MOVE DFHBMFSE TO CLINRDA
010130     MOVE DFHBMFSE TO FECNCDA
010131     MOVE DFHBMFSE TO FECNCMA
010132     MOVE DFHBMFSE TO FECNCAA
010133     MOVE DFHBMFSE TO CLICALA
010134     MOVE DFHBMFSE TO CLICNRA
010135     MOVE DFHBMFSE TO CLIPROA
010136     MOVE DFHBMFSE TO CLILOCA
010137     MOVE DFHBMFSE TO CLIZIPA.
010138 NRO-CLIENTE.
010139     MOVE 1    TO PARAM-TIP
010140     MOVE ZERO TO PARAM-NRO
010141     EXEC CICS READ FILE('PARAM') RIDFLD(PARAM-CLAVE)
010142        INTO(PARAM-REG) RESP(RESPUESTA) KEYLENGTH(9)
010143     END-EXEC
010145     ADD 1 TO PARAM-NRO-CLI
010147     IF CLITPDI = 3
010148         MOVE 2         TO CLITPOO
010149     ELSE
010150         MOVE 1         TO CLITPOO
010151     END-IF
010152     MOVE 1             TO LK-CICLO
010153     MOVE CLITPOI       TO LK-NC-TIP
010154     MOVE PARAM-NRO-CLI TO LK-NCN-NRO
010155     MOVE 0             TO LK-NCN-DIG
010156     EXEC CICS LINK PROGRAM('APCALLDI')
010157         COMMAREA(LK-COMMAREA) LENGTH(11)
010158     END-EXEC
010159     MOVE LK-NC-NRO     TO CLINROO.
010160 RW-NRO-CLIENTE.
010161     MOVE 1    TO PARAM-TIP
010162     MOVE ZERO TO PARAM-NRO
010164     EXEC CICS READ FILE('PARAM') RIDFLD(PARAM-CLAVE)
010165          INTO(PARAM-REG) KEYLENGTH(9) UPDATE
010167     END-EXEC
010168     ADD 1 TO PARAM-NRO-CLI
010169     EXEC CICS REWRITE FILE('PARAM') FROM(PARAM-REG)
010170        LENGTH(80) RESP(RESPUESTA)
010171     END-EXEC.
010172 GUARDAR-CLIENTE.
010173     MOVE CLITPOI       TO CLI-TIP
010174     MOVE CLINROI       TO CLI-NRO
010175     MOVE CLINOMI       TO CLI-NOMBRE
010176     MOVE CLIAPEI       TO CLI-APELLIDO
010177     MOVE CLITPDI (1:1) TO CLI-TIP-DOC
010178     MOVE CLINRDI       TO CLI-NRO-DOC
010179     MOVE FECNCDI       TO CLI-FEC-NACD
010180     MOVE FECNCMI       TO CLI-FEC-NACM
010181     MOVE FECNCAI       TO CLI-FEC-NACA
010182     MOVE CLICALI       TO CLI-CALLE
010183     MOVE CLICNRI       TO CLI-LOC-NUMERO
010184     MOVE CLIPROI       TO CLI-PROVINCIA
010185     MOVE CLILOCI       TO CLI-LOCALIDAD
010186     EXEC CICS WRITE FILE('CLIENTE') FROM(CLI-REGISTRO)
010187           RIDFLD(CLI-CLAVE) LENGTH(132) RESP(RESPUESTA)
010188     END-EXEC.
010189 RETORNO.
010190     MOVE 1 TO LK-CICLO
010191     IF WS-PROG = 2
010192         PERFORM RETORNO-CONSU2
010193     ELSE
010194         PERFORM RETORNO-MENU
010195     END-IF.
010196 RETORNO-MENU.
010197     EXEC CICS XCTL PROGRAM('APMENU') COMMAREA(LK-COMMAREA)
010198         LENGTH(1)
010199     END-EXEC.
010200 RETORNO-CONSU2.
010201     MOVE 1 TO LK-MSG
010202     EXEC CICS XCTL PROGRAM('APCONSU2')
010210         COMMAREA(LK-COMMAREA) LENGTH(12)
010300     END-EXEC.
010892 HELP.
010893     MOVE '1'       TO LK-CICLO-H
010894     MOVE 'APALTAS' TO LK-PROG-H
010895     EXEC CICS XCTL PROGRAM('APHELP')
010896         COMMAREA(LK-COMMAREA-H) LENGTH(10)
010897     END-EXEC
010899     PERFORM CICLO1.
010910 ENCABEZADO.
011000     ACCEPT WS-FECHA  FROM DATE
011100     STRING
011200         WS-FECD
011300         '/'
011400         WS-FECM
011500         '/'
011600         WS-FECA DELIMITED BY SIZE
011700         INTO FECHAO
011800     END-STRING
011900     MOVE EIBTRMID    TO TERMAO
012000     MOVE 'APALTAS'   TO PROGAO.

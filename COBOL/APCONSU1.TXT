000200 IDENTIFICATION DIVISION.
000300 PROGRAM-ID. APCONSU1.
000400 ENVIRONMENT DIVISION.
000500 DATA DIVISION.
000600 WORKING-STORAGE SECTION.
000601 01  RESPUESTA    PIC S9(8) COMP.
000602 01  MSGFATAL     PIC X(20) VALUE 'ERROR DE SEGURIDAD!!'.
000603 01  MSGFIN       PIC X(10) VALUE 'FIN TRANS.'.
000610 01  WS-MSG-ERROR PIC X(17) VALUE 'REGISTRO NO FOUND'.
000700 01  WS-INPUT.
000800     05   WS-TRAN-ID        PIC X(4).
000900     05   WS-MESSAGE-I      PIC X(70).
001000 01  WS-OUTPUT.
001100     05    WS-TEXT          PIC X(8).
001200     05    WS-MESSAGE-O     PIC X(70).
001300 01  WS-MSG-LENGTH          PIC S9(4) COMP.
001310 01  WS-FECHA.
001320     05 WS-FECA             PIC 99.
001330     05 WS-FECM             PIC 99.
001340     05 WS-FECD             PIC 99.
001350 01  WS-PROG                PIC X.
001360
001401 01  LK-COMMAREA.
001402     05 LK-CICLO  PIC 9.
001403     05 LK-NROCLI PIC X(10).
001404     05 LK-MSG    PIC X(31).
001405 01  LK-COMMAREA-H.
001406     05  LK-CICLO-H  PIC 9.
001407     05  LK-PROG-H   PIC X(8).
001408     05  LK-OPCI-H   PIC 9.
001409     05  LK-NRCL-H   PIC 9(10).
001411     COPY DFHAID.
001420     COPY DFHBMSCA.
001421     COPY MAECLIE.
001423     COPY ACONSU1.
001424
001430 LINKAGE SECTION.
001440 01  DFHCOMMAREA.
001450     05 COM-CICLO            PIC 9.
001451     05 COM-NROCLI           PIC 9(10).
001452     05 COM-MSG              PIC X.
001460     05 FILLER               PIC X(88).
001470
001500 PROCEDURE DIVISION.
001510 0000-INICIO.
001511     MOVE LOW-VALUES TO CONSU1O
001515     MOVE DFHCOMMAREA TO LK-COMMAREA
001516     MOVE LK-MSG(1:1) TO WS-PROG
001518
001529     IF EIBCALEN = 0
001530       EXEC CICS SEND FROM(MSGFATAL) LENGTH(20) ERASE
001531       END-EXEC
001532       EXEC CICS ABEND ABCODE('COSA')
001533       END-EXEC
001534     END-IF
001536       EVALUATE LK-CICLO
001537         WHEN 1 PERFORM CICLO1
001538         WHEN 2 PERFORM CICLO2
001539       END-EVALUATE
001540     EXEC CICS RETURN TRANSID('ACO1')
001541           COMMAREA(LK-COMMAREA) LENGTH(12)
001542     END-EXEC.
001543 CICLO1.
001544     PERFORM ENCABEZADO
001545     MOVE LK-NROCLI TO CLI-CLAVE
001546     EXEC CICS READ FILE('CLIENTE') INTO(CLI-REGISTRO)
001547          RIFLD(CLI-CLAVE) KEYLENGTH(10)
001548          RESP(RESPUESTA)
001549     END-EXEC.
001550     IF RESPUESTA NOT = DFHRESP(NORMAL) THEN
001551          MOVE 3 TO LK-CICLO
001552          MOVE 'NO EXISTE EL CLIENTE SOLICITADO' TO LK-MSG
001553          EXEC CICS XCTL PROGRAM('APCONSU') COMMAREA(LK-COMMAREA)
001554               RESP(RESPUESTA) LENGTH(42)
001555          END-EXEC
001556     ELSE
001557        PERFORM MOVER-A-MAPA
001584        EXEC CICS SEND MAP('CONSU1') MAPSET('ACONSU1') ERASE FREEKB
001585        END-EXEC
001586        MOVE 2 TO LK-CICLO
001706     END-IF.
001707 CICLO2.
001708     PERFORM ENCABEZADO
001709     EXEC CICS RECEIVE MAP('CONSU1') MAPSET('ACONSU1')
001710         RESP(RESPUESTA)
001720     END-EXEC
001721     EVALUATE EIBAID
001722         WHEN DFHPF1
001723             MOVE 1 TO LK-CICLO
001724             PERFORM HELP
001729         WHEN DFHPF3
001731             PERFORM RETORNO
001734         WHEN DFHPF4
001735             MOVE 1 TO LK-CICLO
001736             EXEC CICS XCTL PROGRAM('APMENU')
001737                COMMAREA(LK-COMMAREA) LENGTH(1)
001738             END-EXEC
001740         WHEN OTHER
001741            MOVE 1 TO LK-CICLO
001742            PERFORM CICLO1
001743         END-EVALUATE.
001744 MOVER-A-MAPA.
001745        MOVE CLI-TIP            TO CLITPOO
001746        MOVE CLI-NRO            TO CLINROO
001747        MOVE CLI-NOMBRE         TO CLINOMO
001748        IF CLI-TIP = 2
001749            MOVE 'DESCRIPCION:' TO LBLAPEO
001750            MOVE 40             TO CLIAPEL
001751        END-IF
001752        MOVE CLI-APELLIDO       TO CLIAPEO
001753        EVALUATE CLI-TIP-DOC
001754            WHEN  1
001755                MOVE 'DNI'      TO CLITPDO
001756            WHEN  2
001757                MOVE 'CUIT'     TO CLITPDO
001758            WHEN  3
001759                MOVE 'CUIL'     TO CLITPDO
001760        END-EVALUATE
001761        MOVE CLI-NRO-DOC        TO CLINRDO
001762        MOVE CLI-FEC-NACD       TO FECNCDO
001764        MOVE CLI-FEC-NACM       TO FECNCMO
001766        MOVE CLI-FEC-NACA       TO FECNCAO
001770        MOVE CLI-CALLE          TO CLICALO
001771        MOVE CLI-LOC-NUMERO     TO CLICNRO
001772        MOVE CLI-PROVINCIA      TO CLIPROO
001773        MOVE CLI-LOCALIDAD      TO CLILOCO
001774        MOVE CLI-ZIP            TO CLIZIPO.
001775 RETORNO.
001776     MOVE 1 TO LK-CICLO
001777     IF WS-PROG = '2'
001778         PERFORM RETORNO-CONSU2
001779     ELSE
001783         PERFORM RETORNO-CONSU
001794     END-IF.
001795 RETORNO-CONSU.
001798     EXEC CICS XCTL PROGRAM('APCONSU')
001799         COMMAREA(LK-COMMAREA) LENGTH(1)
001800     END-EXEC.
001807 RETORNO-CONSU2.
001808     MOVE 4 TO LK-MSG
001811     EXEC CICS XCTL PROGRAM('APCONSU2')
001812         COMMAREA(LK-COMMAREA) LENGTH(12)
001813     END-EXEC.
001814 HELP.
001815     MOVE 1           TO LK-CICLO-H
001816     MOVE 'APCONSU1'  TO LK-PROG-H
001817     MOVE WS-PROG     TO LK-OPCI-H
001818     MOVE LK-NROCLI   TO LK-NRCL-H
001820     EXEC CICS XCTL PROGRAM('APHELP')
001821         COMMAREA(LK-COMMAREA-H) LENGTH(20)
001822     END-EXEC
001823     PERFORM CICLO1.
001824 ENCABEZADO.
001825     ACCEPT WS-FECHA  FROM DATE
001830     STRING
001900         WS-FECD
002000         '/'
002100         WS-FECM
002200         '/'
002300         WS-FECA DELIMITED BY SIZE
002400         INTO FECHC1O
002500     END-STRING
002600     MOVE EIBTRMID              TO TERMC1O
002700     MOVE 'APCONSU1'            TO PROGC1O
002800     MOVE 'CONSULTA DE CLIENTE' TO LBLTITO.

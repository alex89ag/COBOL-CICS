000200 IDENTIFICATION DIVISION.
000300 PROGRAM-ID. APBAJAS1.
000400 ENVIRONMENT DIVISION.
000500 DATA DIVISION.
000600 WORKING-STORAGE SECTION.
000601 01  RESPUESTA    PIC S9(8) COMP.
000602 01  MSGFATAL     PIC X(20) VALUE 'ERROR DE SEGURIDAD!!'.
000603 01  MSGFIN       PIC X(10) VALUE 'FIN TRANS.'.
000610 01  WS-MSG-ERROR PIC X(17) VALUE 'REGISTRO NO FOUND'.
000700 01  WS-INPUT.
000800     05   WS-TRAN-ID        PIC X(4).
000900     05   WS-MESSAGE-I      PIC X(70).
001000 01  WS-OUTPUT.
001100     05    WS-TEXT          PIC X(8).
001200     05    WS-MESSAGE-O     PIC X(70).
001300 01  WS-MSG-LENGTH          PIC S9(4) COMP.
001310 01  WS-FECHA.
001320     05 WS-FECA             PIC 99.
001330     05 WS-FECM             PIC 99.
001340     05 WS-FECD             PIC 99.
001350 01  WS-PROG                PIC X.
001351 01  WS-CLIENTE             PIC 9(10).
001360
001401 01  LK-COMMAREA.
001402     05 LK-CICLO  PIC 9.
001403     05 LK-NROCLI PIC X(10).
001404     05 LK-MSG    PIC X(31).
001405 01  LK-COMMAREA-H.
001406     05  LK-CICLO-H  PIC 9.
001407     05  LK-PROG-H   PIC X(8).
001408     05  LK-OPCI-H   PIC 9.
001409     05  LK-NRCL-H   PIC 9(10).
001411     COPY DFHAID.
001420     COPY DFHBMSCA.
001421     COPY MAECLIE.
001423     COPY ACONSU1.
001424
001430 LINKAGE SECTION.
001440 01  DFHCOMMAREA.
001450     05 COM-CICLO            PIC 9.
001451     05 COM-NROCLI           PIC 9(10).
001452     05 COM-MSG              PIC X.
001460     05 FILLER               PIC X(88).
001470
001500 PROCEDURE DIVISION.
001510 0000-INICIO.
001511     MOVE LOW-VALUES TO CONSU1O
001515     MOVE DFHCOMMAREA TO LK-COMMAREA
001516     MOVE LK-MSG(1:1) TO WS-PROG
001518
001529     IF EIBCALEN = 0
001530       EXEC CICS SEND FROM(MSGFATAL) LENGTH(20) ERASE
001531       END-EXEC
001532       EXEC CICS ABEND ABCODE('COSA')
001533       END-EXEC
001534     END-IF
001536       EVALUATE LK-CICLO
001537         WHEN 1 PERFORM CICLO1
001538         WHEN 2 PERFORM CICLO2
001539       END-EVALUATE
001540     EXEC CICS RETURN TRANSID('ABJ1')
001541           COMMAREA(LK-COMMAREA) LENGTH(12)
001542     END-EXEC.
001543 CICLO1.
001544     PERFORM ENCABEZADO
001545     MOVE LK-NROCLI TO CLI-CLAVE
001546     EXEC CICS READ FILE('CLIENTE') INTO(CLI-REGISTRO)
001547          RIFLD(CLI-CLAVE) KEYLENGTH(10)
001548          RESP(RESPUESTA)
001549     END-EXEC.
001550     IF RESPUESTA NOT = DFHRESP(NORMAL) THEN
001551          MOVE 3 TO LK-CICLO
001552          MOVE 'NO EXISTE EL CLIENTE SOLICITADO' TO LK-MSG
001553          EXEC CICS XCTL PROGRAM('APBAJAS') COMMAREA(LK-COMMAREA)
001554               RESP(RESPUESTA) LENGTH(42)
001555          END-EXEC
001556     ELSE
001557        PERFORM MOVER-A-MAPA
001584        EXEC CICS SEND MAP('CONSU1') MAPSET('ACONSU1') ERASE FREEKB
001585        END-EXEC
001586        MOVE 2 TO LK-CICLO
001706     END-IF.
001707 CICLO2.
001708     PERFORM ENCABEZADO
001709     EXEC CICS RECEIVE MAP('CONSU1') MAPSET('ACONSU1')
001710         RESP(RESPUESTA)
001720     END-EXEC
001721     EVALUATE EIBAID
001722         WHEN DFHPF1
001723             MOVE 1 TO LK-CICLO
001724             PERFORM HELP
001729         WHEN DFHPF3
001731             PERFORM RETORNO
001734         WHEN DFHPF4
001735             MOVE 1 TO LK-CICLO
001736             EXEC CICS XCTL PROGRAM('APMENU')
001737                 COMMAREA(LK-COMMAREA) LENGTH(1)
001738             END-EXEC
001743         WHEN DFHENTER
001744             EXEC CICS DELETE FILE('CLIENTE')
001745                 RIDFLD(LK-NROCLI) KEYLENGTH(10)
001746                 RESP(RESPUESTA)
001747             END-EXEC
001748             IF RESPUESTA = DFHRESP(NORMAL)
001750                 MOVE 'CLIENTE ELIMINADO              '
001751                 TO LK-MSG
001752                 PERFORM RETORNO-BAJAS-MSG
001755             ELSE
001757                 MOVE 'ERROR AL ELIMINAR EL CLIENTE   '
001758                 TO LK-MSG
001759                 PERFORM RETORNO-BAJAS-MSG
001762             END-IF
001763         WHEN OTHER
001764             MOVE 1 TO LK-CICLO
001765             PERFORM CICLO1
001766         END-EVALUATE.
001767 MOVER-A-MAPA.
001768        MOVE CLI-TIP            TO CLITPOO
001769        MOVE CLI-NRO            TO CLINROO
001770        MOVE CLI-NOMBRE         TO CLINOMO
001771        IF CLI-TIP = 2
001772            MOVE 'DESCRIPCION:' TO LBLAPEO
001773            MOVE 40             TO CLIAPEL
001774        END-IF
001775        MOVE CLI-APELLIDO       TO CLIAPEO
001776        EVALUATE CLI-TIP-DOC
001777            WHEN  1
001778                MOVE 'DNI'      TO CLITPDO
001779            WHEN  2
001780                MOVE 'CUIT'     TO CLITPDO
001781            WHEN  3
001782                MOVE 'CUIL'     TO CLITPDO
001783        END-EVALUATE
001784        MOVE CLI-NRO-DOC        TO CLINRDO
001785        MOVE CLI-FEC-NACD       TO FECNCDO
001787        MOVE CLI-FEC-NACM       TO FECNCMO
001789        MOVE CLI-FEC-NACA       TO FECNCAO
001793        MOVE CLI-CALLE          TO CLICALO
001794        MOVE CLI-LOC-NUMERO     TO CLICNRO
001795        MOVE CLI-PROVINCIA      TO CLIPROO
001796        MOVE CLI-LOCALIDAD      TO CLILOCO.
001797 RETORNO.
001798     MOVE 1 TO LK-CICLO
001799     IF WS-PROG = '2'
001800         PERFORM RETORNO-CONSU2
001801     ELSE
001802         PERFORM RETORNO-BAJAS
001803     END-IF.
001812 RETORNO-BAJAS-MSG.
001813     MOVE 3 TO LK-CICLO
001814     EXEC CICS XCTL PROGRAM('APBAJAS')
001815         COMMAREA(LK-COMMAREA) LENGTH(42)
001816     END-EXEC.
001817 RETORNO-BAJAS.
001818     EXEC CICS XCTL PROGRAM('APBAJAS')
001819         COMMAREA(LK-COMMAREA) LENGTH(1)
001820     END-EXEC.
001821 RETORNO-CONSU2.
001822     MOVE 2 TO LK-MSG
001823     EXEC CICS XCTL PROGRAM('APCONSU2')
001824         COMMAREA(LK-COMMAREA) LENGTH(12)
001825     END-EXEC.
001826 HELP.
001827     MOVE 1           TO LK-CICLO-H
001828     MOVE 'APBAJAS1'  TO LK-PROG-H
001829     MOVE WS-PROG     TO LK-OPCI-H
001830     MOVE LK-NROCLI   TO LK-NRCL-H
001831     EXEC CICS XCTL PROGRAM('APHELP')
001832         COMMAREA(LK-COMMAREA-H) LENGTH(20)
001833     END-EXEC
001834     PERFORM CICLO1.
001835 ENCABEZADO.
001836     ACCEPT WS-FECHA  FROM DATE
001840     STRING
001900         WS-FECD
002000         '/'
002100         WS-FECM
002200         '/'
002300         WS-FECA DELIMITED BY SIZE
002400         INTO FECHC1O
002500     END-STRING
002600     MOVE EIBTRMID              TO TERMC1O
002700     MOVE 'APBAJAS1'            TO PROGC1O
002800     MOVE '  BAJA DE CLIENTE  ' TO LBLTITO.
